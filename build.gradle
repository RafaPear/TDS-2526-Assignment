plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'application'
    id 'org.jetbrains.dokka' version '1.9.20'
}

group = 'pt.isel.reversi'
version = '0.0.1'

application {
    mainClass = 'pt.isel.reversi.MainKt'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.RafaPear:KtFlag:1.4.1'
    implementation 'com.squareup.okio:okio:3.16.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

kotlin {
    jvmToolchain(21)
}

// --- Dokka Configuration ---
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.Platform

tasks.withType(DokkaTask).configureEach {
    dokkaSourceSets.configureEach {
        platform.set(Platform.jvm)
        includes.from('Module.md')
    }
}

// --- Fat Jar ---
tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Creates a fat JAR with all dependencies'
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': application.mainClass.get(),
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with(tasks.jar)
}

// --- Build Configuration ---
tasks.named('build') {
    dependsOn('fatJar')
    finalizedBy('dokkaHtml')  // Gera Javadoc em HTML com hierarquia
}